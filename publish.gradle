apply plugin: 'maven-publish'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            name project.version.toString().endsWith("-SNAPSHOT") ? 'snapshot' : 'release'
            url project.version.toString().endsWith("-SNAPSHOT") ? SNAPSHOT_REPOSITORY : RELEASE_REPOSITORY
            credentials {
                if (project.hasProperty('SONATYPE_SAYNOTOBUGS_USERNAME') && project.hasProperty('SONATYPE_SAYNOTOBUGS_PASSWORD')) {
                    username SONATYPE_SAYNOTOBUGS_USERNAME
                    password SONATYPE_SAYNOTOBUGS_PASSWORD
                }
            }
        }
    }

    publications {
        jar(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom {
                name = project.name
                description = POM_DESCRIPTION
                url = POM_PROJECT_URL
                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEVELOPER_CONNECTION
                }
                licenses {
                    license {
                        name = POM_LICENSE
                        url = POM_LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                        organization = POM_DEVELOPER_ORGANIZATION
                        organizationUrl = POM_DEVELOPER_ORGANIZATION_URL
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
